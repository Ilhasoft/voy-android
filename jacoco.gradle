apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.0'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            '**/*$[0-9].*',
            '**/*Service**',
            '**/*ViewHolder**',
            '**/*Fragment**',
            '**/*Companion**',
            "**/br/com/ilhasoft/voy/models/*",
            "**/br/com/ilhasoft/voy/shared/*",
            "**/br/com/ilhasoft/voy/ui/shared/*",
            "**/br/com/ilhasoft/voy/ui/base/*",
            "**/br/com/ilhasoft/voy/network/urls/*",
            "**/br/com/ilhasoft/voy/network/insterceptors/*",
            "**/br/com/ilhasoft/voy/network/authorization/*"
    ]
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/kotlin"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}